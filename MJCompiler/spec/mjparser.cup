package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
	
	Boolean errorDetected;
   
  
    public void syntax_error(Symbol cur_token) {
		errorDetected = true;
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
		errorDetected = true;
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

 	public void report_fatal_error(String message, Object info) throws java.lang.Exception {
        done_parsing();
      	report_error(message, info);
    }





:}

init with {:
	errorDetected = false;
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


/*Program terminal */
terminal  PROG;

/*Seperator needed terminals*/
terminal SEMI, COMMA, COLON, DOT;

/*Variable modificator needed terminals*/
terminal  CONST;

/*Bracket types needed terminals*/
terminal LBRACE, RBRACE, LPAREN, RPAREN, LSQUARE, RSQUARE;

/*Program control needed terminals*/
terminal IF, ELSE, SWITCH, CASE;

/*Loop needed terminals*/
terminal  CONTINUE, DO, WHILE, BREAK;

/*Class terminals*/
terminal EXTENDS, CLASS, ENUM;

/*Make new variable terminals */
terminal NEW;

/*Operators needed terminals*/
terminal EQUAL_COMPARE, NOT_EQUAL_COMPARE, GREATER, GREATER_EQUAL, LESS, LESS_EQUAL;
terminal AND, OR, INC, DEC, TERNAR, PLUS, MINUS, MUL, DIV, MOD, EQUAL;

/*Predefined functions terminals*/
terminal PRINT, READ;

/*Function needed terminals*/
terminal  VOID, RETURN;



terminal String BOOLEAN;
terminal Character CHAR;
terminal Integer NUMBER;
terminal String IDENT;


//Program
nonterminal ConstVarClassDeclarationList;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgramName, Program;

//Group const, var and class
nonterminal ConstVarClassDeclaration;

//Const variable declaration
nonterminal  ConstDeclaration, ConstList, ConstVariableAssginment;
nonterminal rs.etf.pp1.symboltable.concepts.Struct, ConstType;

//Class declaration
nonterminal ExtendClassOption, ClassDeclaration, OptionalClassMethodsList, OptionalClassVarDeclaration;
nonterminal ClassVariableDeclaration, ClassVarList, ClassVariable, ClassVariableDeclarationList;

//Variable declaration
nonterminal VariableDeclaration, VarList, Variable, VariableDeclarationList;


//GlobalVariable
nonterminal GlobalVariableDeclaration, GlobalVarList, GlobalVariable;


//Method declaration
nonterminal MethodDeclarationList, MethodDeclaration, OptionalMethodsLocalVariablesDeclaration;
nonterminal rs.etf.pp1.symboltable.concepts.Struct ReturnType;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodRetName;
nonterminal FuncCallStart;

//Method formal parameter declaration
nonterminal OptionalMethodFormalParams, FormalParamDeclaration, FormalParamDeclarationList;
nonterminal rs.etf.pp1.symboltable.concepts.Struct OptionalSquareMethodFormalParam;


//Method actual parameter declaration
nonterminal OptionalMethodActualParameters, ActualParamDeclarationList;
nonterminal rs.etf.pp1.symboltable.concepts.Struct FuncCall;

//Designator
nonterminal rs.etf.pp1.symboltable.concepts.Struct  Designator, DesignatorStart, LsquareStart;
nonterminal rs.etf.pp1.symboltable.concepts.Struct DesignatorStatement, DesignatorPart, AssignmentStatement;
nonterminal rs.etf.pp1.symboltable.concepts.Struct SpecifeIdentPart, SpecifeIdentPartList, OptionalSpecifeIdentPartList;
//Operators
nonterminal AddOperator,  MultiplyOperator, RelationalOperator, AssignmentOperator;


//Expression
nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr, Expr1, TernarStart, TernarDivider;
nonterminal OptionalMinusVar;

//Term
nonterminal rs.etf.pp1.symboltable.concepts.Struct TermList, Term;

//Factor
nonterminal rs.etf.pp1.symboltable.concepts.Struct FactorList, Factor, OptionalSquareNewOp;

//Condition
nonterminal Condition, ConditionFact, ConditionTerm, LeftOr, LeftAnd;

//Statement
nonterminal Statement, StatementList, Matched, Unmatched, StartDoStmt, IFCondition, ElseStart, DoCondition, DoConditionStart;


//Variables Type
nonterminal rs.etf.pp1.symboltable.concepts.Struct  Type;

//Case statment
nonterminal CaseStatementList, CaseStatement;

//print oprional variable
nonterminal  rs.etf.pp1.symboltable.concepts.Struct OptionalPrintParam;




/*PROGRAM*/
Program ::= (Program) PROG ProgramName:pObjNode ConstVarClassDeclarationList LBRACE MethodDeclarationList RBRACE
		;

ProgramName ::= (ProgramName) IDENT:programName
			;

/*Const Var Class Declaration List */
ConstVarClassDeclarationList ::= (ConstVarClassDeclList) ConstVarClassDeclarationList  ConstVarClassDeclaration
							| 
								(NoConstVarClassDeclList) /*epsilon*/
							;

ConstVarClassDeclaration ::= (GlobalConstDecl) ConstDeclaration
						|
							(GlobalVariableDecl) GlobalVariableDeclaration
						// |
						// 	(GlobalCLassDecl) ClassDeclaration
						;


/*Const declaration */

ConstDeclaration ::=  (ConstVarDecl) CONST Type:constType ConstList SEMI
				;

ConstList ::= (ConstDeclList)  ConstList COMMA ConstVariableAssginment
		|
		 	  (OneConstDecl) ConstVariableAssginment
		;

ConstVariableAssginment ::= (ConstVariableAssginment) IDENT:VarName EQUAL ConstType
				;

ConstType ::= (ConstIntType) NUMBER:assignedValue
		|
			 (ConstCharType) CHAR:assignedValue
		|
			 (ConstBooleanType) BOOLEAN:assignedValue
		;


/*Class declaration */
// ClassDeclaration ::= (ClassTypeDecl) CLASS IDENT:className ExtendClassOption LBRACE OptionalClassVarDeclaration OptionalClassMethodsList RBRACE
// 				;

// OptionalClassVarDeclaration ::= (ClassVariableDecl) ClassVariableDeclarationList
// 							|
// 								(NoClassVariableDecl) /*epsilon*/
// 					;


// ExtendClassOption ::= (ExtensionClass) EXTENDS Type:classExtendedType
// 				| 
// 					(NoExtensionClass) /*epsilon*/
// 				| 
// 					(ErrorExtenstionClass) error:l
// 					{: parser.report_error("Izvrsen oporavak do { u liniji " + lleft, null);  :}
// 				;
		
// OptionalClassMethodsList ::= (ClassMethods) LBRACE MethodDeclarationList RBRACE
// 						|
// 							(NoClassMethods)/*epsilon*/				
// 						;		

//ClassVariable

// ClassVariableDeclaration ::= (ClassVariableDeclList) Type:classVarType ClassVarList SEMI
// 			|
// 			(ClassVariableErrorBrace) error:l
// 			{: parser.report_error("Izvrsen oporavak do { u liniji " + lleft, null);  :}
// 			|
// 			(ClassVariableErrorDecl) error:l SEMI
// 			{: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null);  :}			
// 			;
		

// ClassVarList ::=  (ClassVariableList) ClassVarList COMMA ClassVariable
// 		| 
// 		    	  (ClassOneVariable) ClassVariable
// 		;	
			
// ClassVariable ::= (ClassArrrayVariable) IDENT:classVarName LSQUARE RSQUARE
// 				|
// 				   (ClassNoArrayVariable) IDENT:clasVarName
// 				;

/*Multiple class variable declarations */
// ClassVariableDeclarationList ::= (ClassVarDeclList) ClassVariableDeclarationList ClassVariableDeclaration
// 			| 
// 					(ClassOneVarDecl) ClassVariableDeclaration
// 			;


/*Variable*/
VariableDeclaration ::= (VariableDecl) Type:varType VarList SEMI
	;
		

VarList ::= (VariableList) VarList COMMA Variable
			| 
			(OneVariable) Variable
			;
			
Variable ::= (ArrrayVariable) IDENT:varName LSQUARE RSQUARE
		|
			(NoArrayVariable) IDENT:varName
		;

/*Multiple variable declarations */
VariableDeclarationList ::= (VarDeclList) VariableDeclarationList VariableDeclaration
			| 
					(OneVarDecl) VariableDeclaration
			;


//GlobalVariable

GlobalVariableDeclaration ::= (GlobalVariableDeclaration) Type:globalVarType GlobalVarList SEMI
			;
		

GlobalVarList ::= (GlobalVariableList) GlobalVarList COMMA GlobalVariable
			| 
				  (GlobalOneVariable) GlobalVariable
			;
			
GlobalVariable ::= (GlobalArrrayVariable) IDENT:globalVarName LSQUARE RSQUARE
		|
			(GlobalNoArrayVariable) IDENT:globalVarName
		|
			(GlobalVariableError) error:l
		{: parser.report_error("Izvrsen oporavak do , ili ; u liniji " + lleft, null);  :}
		;



/*Var type nonterminal */
Type ::= (Type) IDENT:typeName
		;


/*Methods*/
MethodDeclarationList ::= (MethodDeclList) MethodDeclarationList MethodDeclaration
				|
						  (NoMethodDecList) /*epsilon*/				
				;
				
MethodDeclaration ::= (MethodDecl) MethodRetName LPAREN OptionalMethodFormalParams RPAREN OptionalMethodsLocalVariablesDeclaration LBRACE StatementList RBRACE
					;

OptionalMethodsLocalVariablesDeclaration ::= (MethodLocalVariableDecl) VariableDeclarationList
										|    (NoMethodLocalVariableDecl) /*epsilon*/
										;

ReturnType ::= (FunctionReturnType) Type:returnType
			|  
				(NoReturnType) VOID
			;
	
MethodRetName ::= (MethodRetName) ReturnType IDENT:methodName
			;
			



/*Method Formal Parameters*/
OptionalMethodFormalParams ::= (MethodFormalParams) FormalParamDeclarationList
		 				 |
		 					(NoMethodFormalParams) /* epsilon */
		 				 ;
		  
FormalParamDeclarationList ::= (FormalParamDeclList) FormalParamDeclarationList COMMA FormalParamDeclaration
				|
							(OneFormalParamDecl) FormalParamDeclaration
				;
				
FormalParamDeclaration ::= (FormalParamDecl) Type:formalParamType IDENT:formalParamName OptionalSquareMethodFormalParam
						| 
							(ErrorFormalParamDecl) error:l
							{: parser.report_error("Izvrsen oporavak do ; ili ) u liniji " + lleft, null);  :}
					;



OptionalSquareMethodFormalParam ::=  (ArrayFormalParam) LSQUARE RSQUARE
				|
									(NoArrayFormalParam)/*epsilon*/
				;				


		
/*Method Actual parameters*/ 
OptionalMethodActualParameters ::= (MethodActualParams) ActualParamDeclarationList
								| (NoMethodActualParams)/*epsilon*/
								;

ActualParamDeclarationList ::=  (ActualParamDeclList) ActualParamDeclarationList COMMA Expr
							|
								(OneActualParamDecl) Expr
							;
				



/*DESIGNATOR*/

Designator ::= (Designator)  IDENT:variableName OptionalSpecifeIdentPartList
			;

OptionalSpecifeIdentPartList ::= (IdentParts) SpecifeIdentPart
						|
								(NoIdentParts) /*epsilon*/
						;
					
// SpecifeIdentPartList ::= (IdentPartList) SpecifeIdentPartList SpecifeIdentPart
// 					|
// 						(OneIdentPart) SpecifeIdentPart
// 					;


SpecifeIdentPart ::= (ArrayMemberPart) LsquareStart Expr RSQUARE	
					// |
					// (ClassMemberPart) DOT IDENT:classfieldName
					;
					
LsquareStart ::= (LsquareStart) LSQUARE
			;

DesignatorStatement ::=  (AssignmentStmt)  Designator AssignmentStatement
					|	
				    (DesignatorStatementFunctionCall) FuncCall
					|	
				    (IncrementDesignator) Designator INC
					|	
				    (DecrementDesignator) Designator DEC
					;

AssignmentStatement ::= (AssignmentExpr)  AssignmentOperator Expr
					|
					(AssignmentError) error:l
					{: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null);  :}
					;
			

FuncCall ::= (FuncCall) FuncCallStart OptionalMethodActualParameters RPAREN
		;

FuncCallStart ::= (FuncCallStart)  Designator LPAREN 
				;

/*OPERATORS*/

AddOperator ::= (PlusOp) PLUS
			|
				(MinusOp) MINUS
			;

MultiplyOperator ::= (MultiplyOp) MUL
				|
				(DivideOp) DIV
				|
				(ModuloOp) MOD
				;

RelationalOperator ::=  (EqualOp) EQUAL_COMPARE
					| 
						(NotEqualOp) NOT_EQUAL_COMPARE
					|
						(GreaterOp) GREATER
					|
						(GreaterEqualOp) GREATER_EQUAL
					|
						(LessOp) LESS
					|
						(LessEqualOp) LESS_EQUAL
					;

AssignmentOperator ::= (AssignOp) EQUAL
				;



/*Expression*/
Expr ::=    (TernarExpr) TernarStart  Expr1 TernarDivider Expr1
		| 
			(NoTernarExpression) Expr1
		;

TernarStart ::= (TernarStart) Expr1 TERNAR
			;

TernarDivider ::= (TernarDivider) COLON;
			
Expr1 ::= (BasicExpressionWithMinus) MINUS TermList
			|
			(BasicExpressionWithOutMinus) TermList
	 	;
	 
TermList ::= (MultipleTerms) TermList AddOperator Term
		|
		 (OneTerm) Term
		;



/*TERM*/
Term ::= (TermDescription) FactorList;

FactorList ::= (MultipleFactors) FactorList MultiplyOperator Factor
		|
			 (OneFactor) Factor
		;



/*FACTOR*/
Factor ::=  (NumConst) NUMBER
		| 
			(CharConst) CHAR
		| 
			(BoolConst) BOOLEAN
		| 
			(NewOperator) NEW Type  OptionalSquareNewOp
		| 
			(Var) Designator:d
		| 
			(FuncCallFactor) FuncCall
		| 
			(ExprFactor) LPAREN Expr RPAREN
		;

		
OptionalSquareNewOp ::= (NewArrayVariable)  LSQUARE Expr RSQUARE
					| 
					 	(NewNonArrayVariable) /*epsilon*/
 					;


					 
/*CONDITION*/


Condition ::= (ConditionListOr) LeftOr ConditionTerm
			|
			  (OneConditionOr) ConditionTerm
			|
			  (ErrorCondition) error:l /*epsilon*/
			  {: parser.report_error("Izvrsen oporavak do ) u liniji " + lleft, null);  :}
			;


LeftOr ::= (LeftOr)  Condition OR;

ConditionTerm ::=  	(ConditionListAnd) LeftAnd  ConditionFact
				|
					(OneConditionAnd) ConditionFact
				;

LeftAnd ::= (LeftAnd) ConditionTerm AND;



ConditionFact ::= (TwoVariablesCondition)  Expr RelationalOperator Expr
				| 
				   (OneVariableCondition) Expr

				;
/*STATEMENT*/



Statement ::= (MatchedStmt) Matched
		   |
		   	  (UnmatchedStmt) Unmatched
		   ; 
		   
StatementList ::= (StmtList) StatementList Statement
		   |
		   		  (NoStmt) /*epsilon*/
		   ;

Unmatched ::=	(UnmatchedIf) IFCondition RPAREN Statement
		 	|
		 		(UnmatchedIfElse) IFCondition RPAREN Matched ElseStart Unmatched
		 	;


Matched ::= (MatchedDesignator) DesignatorStatement SEMI
		   |
		   (DoStmt) StartDoStmt Statement DoCondition RPAREN SEMI
		   |
		   (PrintStmt) PRINT LPAREN Expr OptionalPrintParam RPAREN SEMI
		   |
		   (ReadStmt) READ LPAREN Designator RPAREN SEMI
		   |
		   (ContinueStmt) CONTINUE SEMI
		   |
		   (BreakStmt) BREAK SEMI
		   |
		   (ReturnExpr) RETURN Expr SEMI
		   |
		   (ReturnNoExpr) RETURN SEMI
		   |
		   (MatchedStatement) IFCondition RPAREN Matched ElseStart Matched
		   |
		   (MultipleStatement) LBRACE StatementList RBRACE
		//    |
		//    (SwithStmt) SWITCH LPAREN Expr RPAREN LBRACE CaseStatementList RBRACE
		   ;
StartDoStmt ::= (StartDoStmt) DO
			;

DoCondition ::= (DoCondition) DoConditionStart Condition;
DoConditionStart ::= (DoConditionStart) WHILE LPAREN;

IFCondition ::= (IFCondition) IF LPAREN Condition
			;

ElseStart ::= (ElseStart) ELSE;

/*Print statement optional parameter */
OptionalPrintParam ::=  (AditionalParamPrint) COMMA NUMBER
					| 
						(NoAditionalParamPrint) /*epsilon*/
					; 


/*CASE STATEMENT*/
// CaseStatementList ::= (CaseStmtList) CaseStatementList CaseStatement
// 				| (NoCaseStmt) /*epsilon*/
// 				;

// CaseStatement ::= (CaseStmt) CASE NUMBER COLON StatementList
// 				;	





					